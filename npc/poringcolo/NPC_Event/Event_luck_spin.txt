-	script	CfgLuckSpin	-1,{
	end;
	
OnInit:
	set $@LS_req_item,7227;
	set $@LS_req_item_amount,10;
	
	set $@LS_slot_default_filename$,"lc0";
	setarray $@LS_slot_filename$[0],
		"lc1", "lc2", "lc3", "lc4", "lc5", "lc6", "lc7", "lc8", "lc9", "lc10",
		"lc11", "lc12", "lc13", "lc14", "lc15", "lc16", "lc17", "lc18", "lc19", "lc20",
		"lc21", "lc22", "lc23", "lc24";
		
	setarray $@LS_slot_item[0],
		985, 984, 7227, 608, 12208, 12210, 7227, 27008, 607, 7539,
		7539, 7227, 607, 5013, 12210, 12208, 12103, 608, 26115, 12103,
		608, 607, 12214, 5286;
		
	// system
	setarray $@LS_size_db[0],
		getarraysize($@LS_slot_filename$);
	end;
}

function	script	LS_NextSlot	{
	set .@img,getarg(0) + 1;
	
	if (.@img >= $@LS_size_db[0])
		set .@img,0;
		
	cutin $@LS_slot_filename$[.@img], 3;
	sleep2 getarg(1);
	
	return .@img;
}

oasis,181,56,5	script	Luck Spin#NPC	4_F_RACING,{
	set .@npc$,"["+strnpcinfo(1)+"]";
	cutin $@LS_slot_default_filename$, 3;
	
	mes .@npc$;
	mes "ยินดีต้อนรับสู่ Luck Spin";
	next;
	if (select("- ต้องการเล่น", "- ยกเลิก") == 2) goto L_Cancel;
	mes .@npc$;
	mes "ท่านต้องมี "+getitemname($@LS_req_item)+" จำนวน "+$@LS_req_item_amount;
	mes "เพื่อใช้ในการเล่น";
	next;
	if (select("- ตกลง", "- ยกเลิก") == 2) goto L_Cancel;
	if (countitem($@LS_req_item) < $@LS_req_item_amount)
	{
		mes .@npc$;
		mes "ท่านมี "+getitemname($@LS_req_item)+" ไม่พอ";
		next;
		goto L_Cancel;
	}
	delitem $@LS_req_item, $@LS_req_item_amount;
	mes .@npc$;
	mes "กดปิดหน้าต่างเพื่อเริ่มเล่น";
	close2;
	pcblockmove getcharid(3),1;
	
	set .@img,0;
	set .@round,rand(5, 10);
	set .@speed,10;
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@mini_round,rand($@LS_size_db[0]/2, $@LS_size_db[0]);
		for (set .@j,0; .@j < .@mini_round; set .@j,.@j+1)
			set .@img,callfunc("LS_NextSlot", .@img, .@speed);
		set .@speed,.@speed+10;
	}

	set .@round,rand($@LS_size_db[0]/2, $@LS_size_db[0]);
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("LS_NextSlot", .@img, .@speed);
		set .@speed,.@speed+50;
	}

	set .@min,($@LS_size_db[0]/2)/2;
	set .@round,rand(.@min, $@LS_size_db[0]/2);
	set .@round_per_50,.@round/2;
	set .@round_per_25,.@round_per_50/2;

	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("LS_NextSlot", .@img, .@speed);

		if (.@i < .@round_per_25)
			set .@speed,.@speed+25;
		else if (.@i < .@round_per_50)
			set .@speed,.@speed+100;
		else
			set .@speed,.@speed+250;
	}
	
	pcblockmove getcharid(3),0;
	set .@prize,$@LS_slot_item[.@img];
	
	if (.@prize == 0)
	{
		mes .@npc$;
		mes "เสียใจด้วยท่านไม่ได้รับรางวัล";
		next;
		goto L_Cancel;
	}
	
	mes .@npc$;
	mes "ยินดีด้วยท่านได้รับ "+getitemname(.@prize)+"";
	getitem .@prize, 1;
	close2;
	cutin "", 255;
	end;
	
L_Cancel:
	mes .@npc$;
	mes "โอกาสหน้าเชิญใหม่";
	close2;
	cutin "", 255;
OnInit:
waitingroom "Luck Spin Mastery",0;
end;
}

